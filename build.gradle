buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.4.0'
        classpath "gradle.plugin.de.inetsoftware:SetupBuilder:3.4.3"
    }
}

version '0.2.0'

apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'nebula.ospackage'
apply plugin: 'de.inetsoftware.setupbuilder'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
	//compile files('lib/thrust-core.jar')
	compile 'org.apache.directory.studio:org.apache.commons.io:2.4'
	
	
	testCompileOnly(
		'org.apiguardian:apiguardian-api:1.0.0'
	)
	testCompile(
            'org.junit.jupiter:junit-jupiter-api:5.0.1'
    )
    testRuntime(
            'org.junit.jupiter:junit-jupiter-engine:5.0.1',
            'org.junit.vintage:junit-vintage-engine:4.12.1',
            'org.junit.platform:junit-platform-launcher:1.0.1',
            'org.junit.platform:junit-platform-runner:1.0.1'
    )
}

jar {
  manifest {
    attributes 'Main-Class': 'br.com.softbox.thrust.main.Main'
  }
  archiveName = 'thrust.jar'
  destinationDir new File("$rootDir/dist")

  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }
  /*removendo do jar itens utilizados para o empacotamento*/
  exclude 'packing'
}

ospackage {
    release = '2'
    os = LINUX
    user = 'root'

    postInstall file('src/main/resources/packing/scripts/postInstall.sh')

    into '/opt/thrust'

    from(jar.outputs.files) {
        into 'lib'
    }
    from('src/main/resources/packing/scripts') {
    	into 'bin'
        fileMode = 0755
    }

    link('/usr/local/bin/thrust', '/opt/thrust/bin/thrust.sh')
}


task buildZip(type: Zip, dependsOn: 'jar') {
    baseName = 'thrust'
    from (tasks.jar.archivePath) {
        into 'lib'
    }
    from ('src/main/resources/packing/scripts/') {
        include '*.cmd'
        into 'bin'
    }
}


setupBuilder {
    vendor = 'Softbox'
    application = 'Thrust'
    description = 'Thrust its an JavaScript interpreter/executor container, in other words, a server-side Javascript (SSJS).'
    icons = "src/main/resources/packing/image/icon.ico"
    licenseFile "src/main/resources/packing/license/license_en.txt"
}


msi {
    wxsTemplate = 'src/main/resources/packing/template/template.wxs'
    from (tasks.jar.archivePath) {
        into 'lib'
    }
    from ('src/main/resources/packing/scripts/') {
        include '*.cmd'
        into 'bin'
    }
}

msi.dependsOn clean
msi.dependsOn jar

/* removendo tasks n√£o utilizadas do plugin SetupBuilder*/
tasks.remove(deb)
tasks.remove(dmg)
tasks.remove(rpm)
